Перем глОбработкаИнтернетЗаказа Экспорт;
Перем глНастройкиИнтернетЗаказа Экспорт;
Перем глОбработкаФР Экспорт;
Перем глСерверТО Экспорт;
Перем глТекущийПользователь Экспорт;
Перем глОбработкаПринтер Экспорт;
Перем глТаблицаТелефонов Экспорт;

Процедура ПриНачалеРаботыСистемы()
	РСНЗ = РегистрыСведений.СостояниеРаботыСистемы.СоздатьНаборЗаписей();
	РСНЗ.Прочитать();
	РСНЗ.Очистить();
	РСНЗ.Записать();
	
	Если ЗначениеЗаполнено(УправлениеПользователями.ТекущийПользователь()) Тогда
		глТекущийПользователь = УправлениеПользователями.ТекущийПользователь();
	КонецЕсли;
	
	УстановитьЗаголовокПриложения("Доставка " + Константы.ФирмаНаименование.Получить());
	
	Если РольДоступна("КассирККМ") Тогда
		ПолучитьОбщуюФорму("ФормаКасса").Открыть();
	КонецЕсли;
	Если РольДоступна("Управляющий") Тогда
		ПолучитьОбщуюФорму("ФормаУправляющий").Открыть();
	КонецЕсли;
	Если РольДоступна("ОператорДоставки") Тогда
		ПолучитьОбщуюФорму("ФормаРабочийСтол").Открыть();
		
		ПодключитьОбработчикОжидания("ПроверкаСоответствия", 60);
	КонецЕсли;
	Если РольДоступна("ОбработчикИнтернетЗаказа") Тогда
		ОбработкаИнтернетЗаказа = Константы.ОбработкаИнтернетЗаказа.Получить();
		Если НЕ ЗначениеЗаполнено(ОбработкаИнтернетЗаказа) Тогда
			Возврат;
		КонецЕсли;
		ДвоичныеДанныеОбработкаИнтернетЗаказа = ОбработкаИнтернетЗаказа.Хранилище.Получить();
		Если НЕ ЗначениеЗаполнено(ДвоичныеДанныеОбработкаИнтернетЗаказа) Тогда
			Возврат;
		КонецЕсли;
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанныеОбработкаИнтернетЗаказа.Записать(ИмяВременногоФайла);
		глОбработкаИнтернетЗаказа = ВнешниеОбработки.Создать(ИмяВременногоФайла);
		//УдалитьФайлы(ИмяВременногоФайла);
		
		глНастройкиИнтернетЗаказа = Константы.НастройкиИнтернетЗаказа.Получить().Получить();
		Если ТипЗнч(глНастройкиИнтернетЗаказа) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		ОбщийМодуль.ВосстановитьНастройкиИзСтруктуры(глНастройкиИнтернетЗаказа, глОбработкаИнтернетЗаказа);
		
		Если Константы.ПериодичностьПроверкиИнтернетЗаказа.Получить() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ИнтернетЗаказ.ПроверитьКлючПодсистемыИнтернетЗаказа() Тогда
			Сообщить("" + ТекущаяДата() + " Начинаем просмотр интернет-заказов с сайта");
			ПодключитьОбработчикОжидания("ОбработчикИнтернетЗаказа", Константы.ПериодичностьПроверкиИнтернетЗаказа.Получить());
		КонецЕсли;
	КонецЕсли;
	
	Если РольДоступна("ОбработчикОбмена") Тогда
		Сообщить("" + ТекущаяДата() + " Начинаем периодический обмен данными с другими подразделениями");
		ПодключитьОбработчикОжидания("ОбработчикОбменаДанными", Константы.ПериодичностьПроверкиИнтернетЗаказа.Получить());
	КонецЕсли;
КонецПроцедуры

Процедура ОбработчикИнтернетЗаказа() Экспорт
	
	ИнтернетЗаказ.ЦентральнаяОбработкаИнтернетЗаказов();
	
КонецПроцедуры

Процедура ОбработчикОбменаДанными() Экспорт
	
	ОбменДанными.ЦентральнаяОбработкаОбменаДанными();
	
КонецПроцедуры

Процедура ПроверкаСоответствия() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Рег.КодНаСайте, Рег.Наименование, Рег.Номенклатура ИЗ РегистрСведений.СоответствиеНоменклатуры КАК Рег ГДЕ Рег.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	ТаблицаСоответствия = Запрос.Выполнить().Выгрузить();
	Если ТаблицаСоответствия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ФормаСоответствия = ПолучитьОбщуюФорму("ФормаСоответствия");
	ФормаСоответствия.ТаблицаСоответствия = ТаблицаСоответствия;
	Если ФормаСоответствия.Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	ФормаСоответствия.ОткрытьМодально();
	
	Для Каждого СтрокаСоответствия ИЗ ФормаСоответствия.ТаблицаСоответствия Цикл
		Если ЗначениеЗаполнено(СтрокаСоответствия.Номенклатура) Тогда
			РСНЗ = РегистрыСведений.СоответствиеНоменклатуры.СоздатьНаборЗаписей();
			РСНЗ.Отбор.КодНаСайте.Установить(СтрокаСоответствия.КодНаСайте);
			РСНЗ.Прочитать();
			РСНЗ.Получить(0).Номенклатура = СтрокаСоответствия.Номенклатура;
			РСНЗ.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьСерверТО() Экспорт

	Если глСерверТО = Неопределено Тогда
		глСерверТО = Обработки.ТОСервер.Создать();
	КонецЕсли;

	Возврат глСерверТО;

КонецФункции